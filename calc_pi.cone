// 2021 Oct 24: doesn't work at the moment
// conec: /newvolume/cone/conec/src/c-compiler/genllvm/genlexpr.c:330: genlFnCallInternal: Assertion `selftypkind == LLVMIntegerTypeKind' failed.
//Compilation failed.

// Calculate and print value of pi
import stdio::*

fn main():
  // Calculate π with 2 terms:
  imm pi2 = pi(2)
  print <- "\u03C0 with 2 terms is: ", pi2, "\n"     // => π with 2 terms is: 3.125
  // Change 2 to 10 for more accurate pi:
  imm pi10 = pi(10)
  print <- "\u03C0 with 10 terms is: ", pi10, "\n"  // => π with 10 terms is: 3.14159
  
// Calculate pi using arc-sine fractional sequence.
// 'nterms' is the number of fractional terms used to estimate pi
// https://en.wikipedia.org/wiki/Approximations_of_%CF%80#Arcsine
fn pi(mut nterms = 10) f64:
  if nterms <= 0:
    return 0d
  
  // Initialize working values
  mut result = 0.5d
  mut seed = 1d
  mut top = 1d
  mut bot = 1d
  mut twos = 2d
  mut term = 0d
  
  while --nterms:
    // Calc a new fraction and add to result
    top *= seed
    bot *= seed + 1d
    twos *= 2d * 2d
    term = top / (bot * (seed + 2d) * twos)
    result += term
    seed += 2d
    print <- "seed: ", seed, " top: ", top, " bot: ", bot,
             " term: ", term, " result: ", result, "\n"

  result * 6d

// =>
// seed: 3 top: 1 bot: 2 term: 0.0208333 result: 0.520833
// π with 2 terms is: 3.125
// seed: 3 top: 1 bot: 2 term: 0.0208333 result: 0.520833
// seed: 5 top: 3 bot: 8 term: 0.00234375 result: 0.523177
// seed: 7 top: 15 bot: 48 term: 0.000348772 result: 0.523526
// seed: 9 top: 105 bot: 384 term: 5.93397e-05 result: 0.523585
// seed: 11 top: 945 bot: 3840 term: 1.09239e-05 result: 0.523596
// seed: 13 top: 10395 bot: 46080 term: 2.11826e-06 result: 0.523598
// seed: 15 top: 135135 bot: 645120 term: 4.26173e-07 result: 0.523599
// seed: 17 top: 2.02702e+06 bot: 1.03219e+07 term: 8.81332e-08 result: 0.523599
// seed: 19 top: 3.44594e+07 bot: 1.85795e+08 term: 1.86188e-08 result: 0.523599
// π with 10 terms is: 3.14159