import stdio::*

fn main():
  imm r = 5.0
  // Use square to calculate the area of a circle
  imm pi = 3.14159
  imm area = pi * square(r)
  print <- area, "\n" // => 78.5397

  add(2, 3)  // returns 5, since a is set to 2 and b to 3
  // add(2)    // Error 1022: Function call requires more arguments than specified
  // add(2.0, 4.0)  // Error 1013: Expression's type does not match declared parameter

  mut number = 3
  weird(5, number)
  print <- number, "\n"   // => 3 // number is still 3, despite weird() changing b to 4

  print <- next(5, 2), "\n" // => 7
  print <- next(4), "\n"    // => 5 (using incr's default value of 1)  
  // next()    // Error 1022: Function call requires more arguments than specified

  mut a i32; mut b i32
  a, b = ceil(8)       // returns 8, 6
  a, b = ceil(3)       // returns 3, 3

  print <- factorial(5), "\n" // => 120
  print <- factorial2(5), "\n" // => 24

  imm m = incr(42)
  print <- m // => 43

fn square(a f32) f32:
  // return a * a
  a * a

fn add(a i32, b i32) i32:
  return a + b

fn next(nbr i32, incr i32 = 1) i32:
  return nbr + incr

fn summult(a f32, b f32) f32:
  imm sum = a + b    // local variable declaration
  imm mult = a * b   // idem
  return sum / mult

fn ceil(x i32) i32, i32:
  if x > 6:
    return x, 6
  // return x, x
  x, x

fn factorial(x i32, prod i32 = 1) i32:
  if x <= 1:
    return prod 
  factorial(x-1, prod*x)

fn factorial2(mut n i32) i32:
  mut result = 1
  if n <= 1:
    result 
  while n > 1:
    result *= n
    n--
  result

fn incr(x i32) i32 inline:
  x+1

fn weird(a i32, mut b i32):
  // ERROR! a is immutable and may not be changed
  // a = 34    // Error 1017: You do not have permission to modify lval
  b = 4     // b may be changed since it is declared 'mut'

